plugins {
    id 'org.springframework.boot' version '2.3.5.RELEASE'
    id 'io.spring.dependency-management' version '1.0.10.RELEASE'
    // Test coverage
    id 'jacoco'

    // Code style
    id 'checkstyle'

    // PMD
    id 'pmd'

    // PITest
    id 'info.solidsoft.pitest' version '1.5.2'
}

subprojects {

    group = 'nl.tudelft.cse.sem'
    version = '0.0.1-SNAPSHOT'

    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'java-library'
    apply plugin: 'checkstyle'
    apply plugin: 'pmd'
    apply plugin: 'jacoco'
    apply plugin: 'info.solidsoft.pitest'

    repositories {
        mavenCentral()
    }

    ext {
        set('springCloudVersion', "Hoxton.SR8")
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:2.3.5.RELEASE")
            mavenBom("org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}")
        }
    }

    checkstyle {
        toolVersion "8.37"
        configFile = file("${rootDir}/config/checkstyle/checkstyle.xml")
    }

    pmd {
        incrementalAnalysis = true
        ruleSetFiles = files("${rootDir}/config/pmd/pmd-rules.xml")
        ruleSets = []
    }

    pitest {
        //adds dependency to org.pitest:pitest-junit5-plugin and sets "testPlugin" to "junit5"
        junit5PluginVersion = '0.12'

        targetClasses = ['nl.tudelft.sem.*']  //by default "${project.group}.*"
        pitestVersion = '1.5.1' //not needed when a default PIT version should be used
        threads = 4
        outputFormats = ['XML', 'HTML']
        timestampedReports = false
        // Ignore the EurekaServer as it has no tests
        if (project.name in ['EurekaServer']) {
            failWhenNoMutations = false
        }
    }

    jacocoTestReport {
        reports {
            xml.required = false
            csv.required = false
            html.outputLocation = layout.buildDirectory.dir('jacocoHtml')
        }
    }

    pitest {
        excludedTestClasses = ['**.repository.**']
    }
}
